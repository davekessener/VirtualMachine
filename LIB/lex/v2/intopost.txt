PARSER InfixToPostfix
VAR NAME = "[a-zA-Z_][a-zA-Z0-9_]*"
VAR NUM  = "[0-9]+(\\.[0-9]+)?([eE][+-]?[0-9]+)?"
BEGIN
	RULE E	= O1 FINAL
	RULE O1	= O2 F1
	RULE F1	= O2 F1
			= EMPTY
	RULE O2	= O3 F2
	RULE F2	= "+" O3 PRINT("+") F2
			= "-" O3 PRINT("-") F2
			= EMPTY
	RULE O3	= O4 F3
	RULE F3	= "*" O4 PRINT("*") F3
			= "/" O4 PRINT("/") F3
			= EMPTY
	RULE O4	= O5 F4
	RULE F4	= "^" O4 PRINT("^")
			= EMPTY
	RULE O5	= "-" O6 PRINT("NEG")
			= O6
	RULE O6	= "sin" O7 PRINT("SIN")
			= "cos" O7 PRINT("COS")
			= "tan" O7 PRINT("TAN")
			= "log" O7 PRINT("LG")
			= "lg" O7 PRINT("LG")
			= "ln" O7 PRINT("LN")
			= "sqrt" O7 PRINT("SQRT")
			= "exp" PRINT("E") O4 PRINT("^")
			= O7
	RULE O7 = O8 F7
	RULE F7 = "%" PRINT("100") PRINT("/")
			= EMPTY
	RULE O8	= "(" O1 ")"
			= ID(R"pi|PI") PRINT("PI")
			= ID(R"[eE]")  PRINT("E")
			= ID(R$NUM) PRINT
END

